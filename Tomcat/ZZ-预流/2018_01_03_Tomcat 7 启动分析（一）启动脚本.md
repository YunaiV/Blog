title: Tomcat 7 å¯åŠ¨åˆ†æï¼ˆä¸€ï¼‰å¯åŠ¨è„šæœ¬
date: 2018-01-03
tag: 
categories: Tomcat
permalink: Tomcat/yuliu/Start-analysis-1-start-script
author: é¢„æµ
from_url: https://juejin.im/post/5a6d15af518825732258f10d
wechat_url: 

-------

æ‘˜è¦: åŸåˆ›å‡ºå¤„ https://juejin.im/post/5a6d1ff6f265da3e243bc1de ã€Œé¢„æµã€æ¬¢è¿è½¬è½½ï¼Œä¿ç•™æ‘˜è¦ï¼Œè°¢è°¢ï¼


-------

![](http://www.iocoder.cn/images/common/wechat_mp_2017_07_31.jpg)

> ğŸ™‚ğŸ™‚ğŸ™‚å…³æ³¨**å¾®ä¿¡å…¬ä¼—å·ï¼šã€èŠ‹é“æºç ã€‘**æœ‰ç¦åˆ©ï¼š
> 1. RocketMQ / MyCAT / Sharding-JDBC **æ‰€æœ‰**æºç åˆ†ææ–‡ç« åˆ—è¡¨
> 2. RocketMQ / MyCAT / Sharding-JDBC **ä¸­æ–‡æ³¨é‡Šæºç  GitHub åœ°å€**
> 3. æ‚¨å¯¹äºæºç çš„ç–‘é—®æ¯æ¡ç•™è¨€**éƒ½**å°†å¾—åˆ°**è®¤çœŸ**å›å¤ã€‚**ç”šè‡³ä¸çŸ¥é“å¦‚ä½•è¯»æºç ä¹Ÿå¯ä»¥è¯·æ•™å™¢**ã€‚
> 4. **æ–°çš„**æºç è§£ææ–‡ç« **å®æ—¶**æ”¶åˆ°é€šçŸ¥ã€‚**æ¯å‘¨æ›´æ–°ä¸€ç¯‡å·¦å³**ã€‚
> 5. **è®¤çœŸçš„**æºç äº¤æµå¾®ä¿¡ç¾¤ã€‚

-------


åœ¨ä¸Šä¸€ç¯‡æ–‡ç« [ã€Šæºä»£ç è¿è¡Œç¯å¢ƒæ­å»ºã€‹](http://www.iocoder.cn/Tomcat/yuliu/build-debugging-environment)é‡Œæ˜¯é€šè¿‡ç›´æ¥è¿è¡Œ BootStarp çš„ main å‡½æ•°æ¥å¯åŠ¨çš„ï¼Œåªæ˜¯åŠ äº†ä¸€ä¸ª catalina.home çš„ç³»ç»Ÿå±æ€§ã€‚è€Œæ­£å¸¸æƒ…å†µä¸‹å¯åŠ¨ Tomcat æ˜¯é€šè¿‡è¿è¡Œè„šæœ¬çš„æ–¹å¼ï¼Œè¿™ä¸ªå°±æ¶‰åŠåˆ°å»ºç«‹å·¥ç¨‹æ—¶æ‹·è´è¿‡æ¥çš„ script ç›®å½•ä¸‹çš„ä¸€å †è„šæœ¬æ–‡ä»¶äº†ã€‚



![img](https://user-gold-cdn.xitu.io/2018/1/28/1613a2005fb1bd1a?imageView2/0/w/1280/h/960/format/jpeg/ignore-error/1)

 ä»¥æˆ‘çš„ windows ç³»ç»Ÿä¸¾ä¾‹ï¼Œå®é™…ä¸Šæœ€ç»ˆæ˜¯æ‰§è¡Œ startup.bat è¿™ä¸ªæ‰¹å¤„ç†æ–‡ä»¶æ¥å¯åŠ¨ tomcat çš„ã€‚ é‚£ä¹ˆå¯åŠ¨åˆ†æå°±å†²è¿™ä¸ªæ–‡ä»¶å¼€å§‹å§ï¼š



# ä¸€ã€åˆ†æ startup.bat æ–‡ä»¶

startup.bat :

```Bash
     1	@echo off
     2	rem Licensed to the Apache Software Foundation (ASF) under one or more
     3	rem contributor license agreements.  See the NOTICE file distributed with
     4	rem this work for additional information regarding copyright ownership.
     5	rem The ASF licenses this file to You under the Apache License, Version 2.0
     6	rem (the "License"); you may not use this file except in compliance with
     7	rem the License.  You may obtain a copy of the License at
     8	rem
     9	rem     http://www.apache.org/licenses/LICENSE-2.0
    10	rem
    11	rem Unless required by applicable law or agreed to in writing, software
    12	rem distributed under the License is distributed on an "AS IS" BASIS,
    13	rem WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    14	rem See the License for the specific language governing permissions and
    15	rem limitations under the License.
    16
    17	if "%OS%" == "Windows_NT" setlocal
    18	rem ---------------------------------------------------------------------------
    19	rem Start script for the CATALINA Server
    20	rem
    21	rem $Id: startup.bat 895392 2010-01-03 14:02:31Z kkolinko $
    22	rem ---------------------------------------------------------------------------
    23
    24	rem Guess CATALINA_HOME if not defined
    25	set "CURRENT_DIR=%cd%"
    26	if not "%CATALINA_HOME%" == "" goto gotHome
    27	set "CATALINA_HOME=%CURRENT_DIR%"
    28	if exist "%CATALINA_HOME%\bin\catalina.bat" goto okHome
    29	cd ..
    30	set "CATALINA_HOME=%cd%"
    31	cd "%CURRENT_DIR%"
    32	:gotHome
    33	if exist "%CATALINA_HOME%\bin\catalina.bat" goto okHome
    34	echo The CATALINA_HOME environment variable is not defined correctly
    35	echo This environment variable is needed to run this program
    36	goto end
    37	:okHome
    38
    39	set "EXECUTABLE=%CATALINA_HOME%\bin\catalina.bat"
    40
    41	rem Check that target executable exists
    42	if exist "%EXECUTABLE%" goto okExec
    43	echo Cannot find "%EXECUTABLE%"
    44	echo This file is needed to run this program
    45	goto end
    46	:okExec
    47
    48	rem Get remaining unshifted command line arguments and save them in the
    49	set CMD_LINE_ARGS=
    50	:setArgs
    51	if ""%1""=="""" goto doneSetArgs
    52	set CMD_LINE_ARGS=%CMD_LINE_ARGS% %1
    53	shift
    54	goto setArgs
    55	:doneSetArgs
    56
    57	call "%EXECUTABLE%" start %CMD_LINE_ARGS%
    58
    59	:end
```

ç¬¬ 1 è¡Œ `@echo off` çš„æ˜¯å…³é—­æ˜¾ç¤ºå‘½ä»¤è¡Œè‡ªèº«çš„æ„æ€ï¼Œæ¥ä¸‹æ¥ä¸€å¤§å † rem æ˜¯æ³¨é‡Šï¼Œç¬¬ 17 è¡Œ `if "%OS%" == "Windows_NT" setlocal` æ˜¯è¦ä¸´æ—¶ä¿®æ”¹ç³»ç»Ÿå˜é‡ï¼Œæ–¹ä¾¿æ‰¹å¤„ç†è¿è¡Œã€‚ç¬¬ 26 åˆ° 33 è¡Œçš„ä½œç”¨æ˜¯è®¾ç½® `CATALINA_HOME` ç¯å¢ƒå˜é‡ï¼Œå¦‚æœæ“ä½œç³»ç»Ÿä¸­æ²¡è®¾ç½®ï¼Œåˆ™å°†å½“å‰è¿è¡Œè„šæœ¬çš„ç›®å½•ä½œä¸ºè¯¥ç¯å¢ƒå˜é‡çš„å€¼ï¼Œæœ€ç»ˆéƒ½ä¼šè·³è½¬åˆ° 37 è¡Œã€‚39 åˆ° 46 è¡ŒéªŒè¯ `CATALINA_HOME` å˜é‡æ‰€ç¤ºç›®å½•ä¸‹çš„ catalina.bat æ–‡ä»¶æ˜¯å¦å­˜åœ¨ï¼Œä¸å­˜åœ¨åˆ™æ‰¹å¤„ç†ç›´æ¥ç»“æŸã€‚48 åˆ° 55 è¡Œå°†è¿è¡Œè„šæœ¬å‰å¦‚æœè®¾ç½®äº†å…¶ä»–å‚æ•°ï¼Œå°†å‚æ•°ä¿å­˜åˆ° `CMD_LINE_ARGS` å˜é‡ä¸­ã€‚æœ€åç¬¬ 57 è¡Œæ‰§è¡Œ catalina.bat æ‰¹å¤„ç†æ–‡ä»¶ï¼Œæ³¨æ„ï¼Œè¯¥è¡Œåé¢è·Ÿç€ä¸¤ä¸ªå‚æ•°ï¼Œç¬¬ 1 ä¸ªå‚æ•°æ˜¯ startã€‚

# äºŒã€åˆ†æ catalina.bat æ–‡ä»¶

```Bash
     1	@echo off
     2	rem Licensed to the Apache Software Foundation (ASF) under one or more
     3	rem contributor license agreements.  See the NOTICE file distributed with
     4	rem this work for additional information regarding copyright ownership.
     5	rem The ASF licenses this file to You under the Apache License, Version 2.0
     6	rem (the "License"); you may not use this file except in compliance with
     7	rem the License.  You may obtain a copy of the License at
     8	rem
     9	rem     http://www.apache.org/licenses/LICENSE-2.0
    10	rem
    11	rem Unless required by applicable law or agreed to in writing, software
    12	rem distributed under the License is distributed on an "AS IS" BASIS,
    13	rem WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    14	rem See the License for the specific language governing permissions and
    15	rem limitations under the License.
    16
    17	if "%OS%" == "Windows_NT" setlocal
    18	rem ---------------------------------------------------------------------------
    19	rem Start/Stop Script for the CATALINA Server
    20	rem
    21	rem Environment Variable Prerequisites
    22	rem
    23	rem   Do not set the variables in this script. Instead put them into a script
    24	rem   setenv.bat in CATALINA_BASE/bin to keep your customizations separate.
    25	rem
    26	rem   CATALINA_HOME   May point at your Catalina "build" directory.
    27	rem
    28	rem   CATALINA_BASE   (Optional) Base directory for resolving dynamic portions
    29	rem                   of a Catalina installation.  If not present, resolves to
    30	rem                   the same directory that CATALINA_HOME points to.
    31	rem
    32	rem   CATALINA_OPTS   (Optional) Java runtime options used when the "start",
    33	rem                   "run" or "debug" command is executed.
    34	rem                   Include here and not in JAVA_OPTS all options, that should
    35	rem                   only be used by Tomcat itself, not by the stop process,
    36	rem                   the version command etc.
    37	rem                   Examples are heap size, GC logging, JMX ports etc.
    38	rem
    39	rem   CATALINA_TMPDIR (Optional) Directory path location of temporary directory
    40	rem                   the JVM should use (java.io.tmpdir).  Defaults to
    41	rem                   %CATALINA_BASE%\temp.
    42	rem
    43	rem   JAVA_HOME       Must point at your Java Development Kit installation.
    44	rem                   Required to run the with the "debug" argument.
    45	rem
    46	rem   JRE_HOME        Must point at your Java Runtime installation.
    47	rem                   Defaults to JAVA_HOME if empty. If JRE_HOME and JAVA_HOME
    48	rem                   are both set, JRE_HOME is used.
    49	rem
    50	rem   JAVA_OPTS       (Optional) Java runtime options used when any command
    51	rem                   is executed.
    52	rem                   Include here and not in CATALINA_OPTS all options, that
    53	rem                   should be used by Tomcat and also by the stop process,
    54	rem                   the version command etc.
    55	rem                   Most options should go into CATALINA_OPTS.
    56	rem
    57	rem   JAVA_ENDORSED_DIRS (Optional) Lists of of semi-colon separated directories
    58	rem                   containing some jars in order to allow replacement of APIs
    59	rem                   created outside of the JCP (i.e. DOM and SAX from W3C).
    60	rem                   It can also be used to update the XML parser implementation.
    61	rem                   Defaults to $CATALINA_HOME/endorsed.
    62	rem
    63	rem   JPDA_TRANSPORT  (Optional) JPDA transport used when the "jpda start"
    64	rem                   command is executed. The default is "dt_socket".
    65	rem
    66	rem   JPDA_ADDRESS    (Optional) Java runtime options used when the "jpda start"
    67	rem                   command is executed. The default is 8000.
    68	rem
    69	rem   JPDA_SUSPEND    (Optional) Java runtime options used when the "jpda start"
    70	rem                   command is executed. Specifies whether JVM should suspend
    71	rem                   execution immediately after startup. Default is "n".
    72	rem
    73	rem   JPDA_OPTS       (Optional) Java runtime options used when the "jpda start"
    74	rem                   command is executed. If used, JPDA_TRANSPORT, JPDA_ADDRESS,
    75	rem                   and JPDA_SUSPEND are ignored. Thus, all required jpda
    76	rem                   options MUST be specified. The default is:
    77	rem
    78	rem                   -agentlib:jdwp=transport=%JPDA_TRANSPORT%,
    79	rem                       address=%JPDA_ADDRESS%,server=y,suspend=%JPDA_SUSPEND%
    80	rem
    81	rem   LOGGING_CONFIG  (Optional) Override Tomcat's logging config file
    82	rem                   Example (all one line)
    83	rem                   set LOGGING_CONFIG="-Djava.util.logging.config.file=%CATALINA_BASE%\conf\logging.properties"
    84	rem
    85	rem   LOGGING_MANAGER (Optional) Override Tomcat's logging manager
    86	rem                   Example (all one line)
    87	rem                   set LOGGING_MANAGER="-Djava.util.logging.manager=org.apache.juli.ClassLoaderLogManager"
    88	rem
    89	rem   TITLE           (Optional) Specify the title of Tomcat window. The default
    90	rem                   TITLE is Tomcat if it's not specified.
    91	rem                   Example (all one line)
    92	rem                   set TITLE=Tomcat.Cluster#1.Server#1 [%DATE% %TIME%]
    93	rem
    94	rem
    95	rem
    96	rem $Id: catalina.bat 1344732 2012-05-31 14:08:02Z kkolinko $
    97	rem ---------------------------------------------------------------------------
    98
    99	rem Suppress Terminate batch job on CTRL+C
   100	if not ""%1"" == ""run"" goto mainEntry
   101	if "%TEMP%" == "" goto mainEntry
   102	if exist "%TEMP%\%~nx0.run" goto mainEntry
   103	echo Y>"%TEMP%\%~nx0.run"
   104	if not exist "%TEMP%\%~nx0.run" goto mainEntry
   105	echo Y>"%TEMP%\%~nx0.Y"
   106	call "%~f0" %* <"%TEMP%\%~nx0.Y"
   107	rem Use provided errorlevel
   108	set RETVAL=%ERRORLEVEL%
   109	del /Q "%TEMP%\%~nx0.Y" >NUL 2>&1
   110	exit /B %RETVAL%
   111	:mainEntry
   112	del /Q "%TEMP%\%~nx0.run" >NUL 2>&1
   113
   114	rem Guess CATALINA_HOME if not defined
   115	set "CURRENT_DIR=%cd%"
   116	if not "%CATALINA_HOME%" == "" goto gotHome
   117	set "CATALINA_HOME=%CURRENT_DIR%"
   118	if exist "%CATALINA_HOME%\bin\catalina.bat" goto okHome
   119	cd ..
   120	set "CATALINA_HOME=%cd%"
   121	cd "%CURRENT_DIR%"
   122	:gotHome
   123
   124	if exist "%CATALINA_HOME%\bin\catalina.bat" goto okHome
   125	echo The CATALINA_HOME environment variable is not defined correctly
   126	echo This environment variable is needed to run this program
   127	goto end
   128	:okHome
   129
   130	rem Copy CATALINA_BASE from CATALINA_HOME if not defined
   131	if not "%CATALINA_BASE%" == "" goto gotBase
   132	set "CATALINA_BASE=%CATALINA_HOME%"
   133	:gotBase
   134
   135	rem Ensure that any user defined CLASSPATH variables are not used on startup,
   136	rem but allow them to be specified in setenv.bat, in rare case when it is needed.
   137	set CLASSPATH=
   138
   139	rem Get standard environment variables
   140	if not exist "%CATALINA_BASE%\bin\setenv.bat" goto checkSetenvHome
   141	call "%CATALINA_BASE%\bin\setenv.bat"
   142	goto setenvDone
   143	:checkSetenvHome
   144	if exist "%CATALINA_HOME%\bin\setenv.bat" call "%CATALINA_HOME%\bin\setenv.bat"
   145	:setenvDone
   146
   147	rem Get standard Java environment variables
   148	if exist "%CATALINA_HOME%\bin\setclasspath.bat" goto okSetclasspath
   149	echo Cannot find "%CATALINA_HOME%\bin\setclasspath.bat"
   150	echo This file is needed to run this program
   151	goto end
   152	:okSetclasspath
   153	call "%CATALINA_HOME%\bin\setclasspath.bat" %1
   154	if errorlevel 1 goto end
   155
   156	rem Add on extra jar file to CLASSPATH
   157	rem Note that there are no quotes as we do not want to introduce random
   158	rem quotes into the CLASSPATH
   159	if "%CLASSPATH%" == "" goto emptyClasspath
   160	set "CLASSPATH=%CLASSPATH%;"
   161	:emptyClasspath
   162	set "CLASSPATH=%CLASSPATH%%CATALINA_HOME%\bin\bootstrap.jar"
   163
   164	if not "%CATALINA_TMPDIR%" == "" goto gotTmpdir
   165	set "CATALINA_TMPDIR=%CATALINA_BASE%\temp"
   166	:gotTmpdir
   167
   168	rem Add tomcat-juli.jar to classpath
   169	rem tomcat-juli.jar can be over-ridden per instance
   170	if not exist "%CATALINA_BASE%\bin\tomcat-juli.jar" goto juliClasspathHome
   171	set "CLASSPATH=%CLASSPATH%;%CATALINA_BASE%\bin\tomcat-juli.jar"
   172	goto juliClasspathDone
   173	:juliClasspathHome
   174	set "CLASSPATH=%CLASSPATH%;%CATALINA_HOME%\bin\tomcat-juli.jar"
   175	:juliClasspathDone
   176
   177	if not "%LOGGING_CONFIG%" == "" goto noJuliConfig
   178	set LOGGING_CONFIG=-Dnop
   179	if not exist "%CATALINA_BASE%\conf\logging.properties" goto noJuliConfig
   180	set LOGGING_CONFIG=-Djava.util.logging.config.file="%CATALINA_BASE%\conf\logging.properties"
   181	:noJuliConfig
   182	set JAVA_OPTS=%JAVA_OPTS% %LOGGING_CONFIG%
   183
   184	if not "%LOGGING_MANAGER%" == "" goto noJuliManager
   185	set LOGGING_MANAGER=-Djava.util.logging.manager=org.apache.juli.ClassLoaderLogManager
   186	:noJuliManager
   187	set JAVA_OPTS=%JAVA_OPTS% %LOGGING_MANAGER%
   188
   189	rem ----- Execute The Requested Command ---------------------------------------
   190
   191	echo Using CATALINA_BASE:   "%CATALINA_BASE%"
   192	echo Using CATALINA_HOME:   "%CATALINA_HOME%"
   193	echo Using CATALINA_TMPDIR: "%CATALINA_TMPDIR%"
   194	if ""%1"" == ""debug"" goto use_jdk
   195	echo Using JRE_HOME:        "%JRE_HOME%"
   196	goto java_dir_displayed
   197	:use_jdk
   198	echo Using JAVA_HOME:       "%JAVA_HOME%"
   199	:java_dir_displayed
   200	echo Using CLASSPATH:       "%CLASSPATH%"
   201
   202	set _EXECJAVA=%_RUNJAVA%
   203	set MAINCLASS=org.apache.catalina.startup.Bootstrap
   204	set ACTION=start
   205	set SECURITY_POLICY_FILE=
   206	set DEBUG_OPTS=
   207	set JPDA=
   208
   209	if not ""%1"" == ""jpda"" goto noJpda
   210	set JPDA=jpda
   211	if not "%JPDA_TRANSPORT%" == "" goto gotJpdaTransport
   212	set JPDA_TRANSPORT=dt_socket
   213	:gotJpdaTransport
   214	if not "%JPDA_ADDRESS%" == "" goto gotJpdaAddress
   215	set JPDA_ADDRESS=8000
   216	:gotJpdaAddress
   217	if not "%JPDA_SUSPEND%" == "" goto gotJpdaSuspend
   218	set JPDA_SUSPEND=n
   219	:gotJpdaSuspend
   220	if not "%JPDA_OPTS%" == "" goto gotJpdaOpts
   221	set JPDA_OPTS=-agentlib:jdwp=transport=%JPDA_TRANSPORT%,address=%JPDA_ADDRESS%,server=y,suspend=%JPDA_SUSPEND%
   222	:gotJpdaOpts
   223	shift
   224	:noJpda
   225
   226	if ""%1"" == ""debug"" goto doDebug
   227	if ""%1"" == ""run"" goto doRun
   228	if ""%1"" == ""start"" goto doStart
   229	if ""%1"" == ""stop"" goto doStop
   230	if ""%1"" == ""configtest"" goto doConfigTest
   231	if ""%1"" == ""version"" goto doVersion
   232
   233	echo Usage:  catalina ( commands ... )
   234	echo commands:
   235	echo   debug             Start Catalina in a debugger
   236	echo   debug -security   Debug Catalina with a security manager
   237	echo   jpda start        Start Catalina under JPDA debugger
   238	echo   run               Start Catalina in the current window
   239	echo   run -security     Start in the current window with security manager
   240	echo   start             Start Catalina in a separate window
   241	echo   start -security   Start in a separate window with security manager
   242	echo   stop              Stop Catalina
   243	echo   configtest        Run a basic syntax check on server.xml
   244	echo   version           What version of tomcat are you running?
   245	goto end
   246
   247	:doDebug
   248	shift
   249	set _EXECJAVA=%_RUNJDB%
   250	set DEBUG_OPTS=-sourcepath "%CATALINA_HOME%\..\..\java"
   251	if not ""%1"" == ""-security"" goto execCmd
   252	shift
   253	echo Using Security Manager
   254	set "SECURITY_POLICY_FILE=%CATALINA_BASE%\conf\catalina.policy"
   255	goto execCmd
   256
   257	:doRun
   258	shift
   259	if not ""%1"" == ""-security"" goto execCmd
   260	shift
   261	echo Using Security Manager
   262	set "SECURITY_POLICY_FILE=%CATALINA_BASE%\conf\catalina.policy"
   263	goto execCmd
   264
   265	:doStart
   266	shift
   267	if not "%OS%" == "Windows_NT" goto noTitle
   268	if "%TITLE%" == "" set TITLE=Tomcat
   269	set _EXECJAVA=start "%TITLE%" %_RUNJAVA%
   270	goto gotTitle
   271	:noTitle
   272	set _EXECJAVA=start %_RUNJAVA%
   273	:gotTitle
   274	if not ""%1"" == ""-security"" goto execCmd
   275	shift
   276	echo Using Security Manager
   277	set "SECURITY_POLICY_FILE=%CATALINA_BASE%\conf\catalina.policy"
   278	goto execCmd
   279
   280	:doStop
   281	shift
   282	set ACTION=stop
   283	set CATALINA_OPTS=
   284	goto execCmd
   285
   286	:doConfigTest
   287	shift
   288	set ACTION=configtest
   289	set CATALINA_OPTS=
   290	goto execCmd
   291
   292	:doVersion
   293	%_EXECJAVA% -classpath "%CATALINA_HOME%\lib\catalina.jar" org.apache.catalina.util.ServerInfo
   294	goto end
   295
   296
   297	:execCmd
   298	rem Get remaining unshifted command line arguments and save them in the
   299	set CMD_LINE_ARGS=
   300	:setArgs
   301	if ""%1""=="""" goto doneSetArgs
   302	set CMD_LINE_ARGS=%CMD_LINE_ARGS% %1
   303	shift
   304	goto setArgs
   305	:doneSetArgs
   306
   307	rem Execute Java with the applicable properties
   308	if not "%JPDA%" == "" goto doJpda
   309	if not "%SECURITY_POLICY_FILE%" == "" goto doSecurity
   310	%_EXECJAVA% %JAVA_OPTS% %CATALINA_OPTS% %DEBUG_OPTS% -Djava.endorsed.dirs="%JAVA_ENDORSED_DIRS%" -classpath "%CLASSPATH%" -Dcatalina.base="%CATALINA_BASE%" -Dcatalina.home="%CATALINA_HOME%" -Djava.io.tmpdir="%CATALINA_TMPDIR%" %MAINCLASS% %CMD_LINE_ARGS% %ACTION%
   311	goto end
   312	:doSecurity
   313	%_EXECJAVA% %JAVA_OPTS% %CATALINA_OPTS% %DEBUG_OPTS% -Djava.endorsed.dirs="%JAVA_ENDORSED_DIRS%" -classpath "%CLASSPATH%" -Djava.security.manager -Djava.security.policy=="%SECURITY_POLICY_FILE%" -Dcatalina.base="%CATALINA_BASE%" -Dcatalina.home="%CATALINA_HOME%" -Djava.io.tmpdir="%CATALINA_TMPDIR%" %MAINCLASS% %CMD_LINE_ARGS% %ACTION%
   314	goto end
   315	:doJpda
   316	if not "%SECURITY_POLICY_FILE%" == "" goto doSecurityJpda
   317	%_EXECJAVA% %JAVA_OPTS% %CATALINA_OPTS% %JPDA_OPTS% %DEBUG_OPTS% -Djava.endorsed.dirs="%JAVA_ENDORSED_DIRS%" -classpath "%CLASSPATH%" -Dcatalina.base="%CATALINA_BASE%" -Dcatalina.home="%CATALINA_HOME%" -Djava.io.tmpdir="%CATALINA_TMPDIR%" %MAINCLASS% %CMD_LINE_ARGS% %ACTION%
   318	goto end
   319	:doSecurityJpda
   320	%_EXECJAVA% %JAVA_OPTS% %CATALINA_OPTS% %JPDA_OPTS% %DEBUG_OPTS% -Djava.endorsed.dirs="%JAVA_ENDORSED_DIRS%" -classpath "%CLASSPATH%" -Djava.security.manager -Djava.security.policy=="%SECURITY_POLICY_FILE%" -Dcatalina.base="%CATALINA_BASE%" -Dcatalina.home="%CATALINA_HOME%" -Djava.io.tmpdir="%CATALINA_TMPDIR%" %MAINCLASS% %CMD_LINE_ARGS% %ACTION%
   321	goto end
   322
   323	:end

```

1 åˆ° 99 è¡Œä¸€å¤§å †æ³¨é‡Šè¯´æ˜å°±ä¸çœ‹äº†ï¼Œ100 åˆ° 110 è¡Œä¸€å † run æ–‡ä»¶æ ¡éªŒä¹‹ç±»çš„ä¸€èˆ¬ç”¨ä¸åˆ°ï¼Œç›´æ¥è·³åˆ° 111 è¡Œï¼Œæ¥ä¸‹æ¥å…ˆåˆ é™¤ä¸Šé¢å¯èƒ½äº§ç”Ÿçš„ä¸­é—´æ–‡ä»¶ï¼Œæ¥ç€æŒ‰ç…§æ­£å¸¸æµç¨‹ï¼Œåœ¨ä¸Šé¢çš„ startup.bat æ–‡ä»¶ä¸­å·²ç»è®¾ç½®è¿‡`CATALINA_HOME`å˜é‡ï¼Œå¹¶ä¸”æ ¡éªŒäº†æ”¹å˜é‡ä¸‹ bin\catalina.bat æ–‡ä»¶å·²ç»å­˜åœ¨ï¼ˆæˆ‘çŒœä½œè€…å†™è¿™ä¸€æ®µæ˜¯é˜²æ­¢ç”¨æˆ·ç›´æ¥è¿è¡Œ catalina.bat æ–‡ä»¶ï¼‰ã€‚ç»è¿‡è¿™äº›åˆ¤æ–­åè·³è½¬åˆ° 128 è¡Œï¼Œ130 åˆ° 133 è¡Œè®¾ç½®`CATALINA_BASE`å˜é‡ï¼Œä¸­é—´å‡ è¡Œæ³¨é‡Šç•¥è¿‡ï¼Œ139 åˆ° 144 è¡Œå¦‚æœæœ‰ setenv.bat æ–‡ä»¶åˆ™æ‰§è¡Œä¸€ä¸‹è¯¥æ–‡ä»¶ã€‚147 åˆ° 153 è¡Œæ‰§è¡Œ setclasspath.bat æ–‡ä»¶ï¼ˆè¯¥æ–‡ä»¶åœ¨åŒçº§ç›®å½•ä¸‹å·²ç»æä¾›ï¼Œå…·ä½“è„šæœ¬ä¸‹é¢å·²ç»è´´å‡ºæ¥äº†ï¼Œå¯ä»¥è¯»ä¸€ä¸‹ï¼Œè¿™é‡Œä¸å†åšåˆ†æã€‚ä¸»è¦åŠŸèƒ½æ ¡éªŒæ˜¯å¦è®¾ç½®`JAVA_HOME`å˜é‡ï¼Œæ²¡æœ‰åˆ™åœ¨æ§åˆ¶å°æ˜¾ç¤ºå‡ºæç¤ºä¿¡æ¯ã€‚æ³¨æ„è¯¥æ–‡ä»¶ä¸­ç¬¬ 79 è¡Œå°†ä¼šè®¾ç½®`_RUNJAVA`å˜é‡ï¼Œè¯¥å˜é‡çš„å€¼å³`JAVA_HOME`æˆ–`JRE_HOME`çš„ bin ç›®å½•ä¸‹çš„ java.exe æ–‡ä»¶ï¼‰ã€‚ä» 156 åˆ° 224 è¡Œæ ¡éªŒå¹¶è®¾ç½®ä¸€å †å˜é‡ï¼ˆæ³¨æ„ç¬¬ 202ã€203 å’Œ 204 è¡Œï¼Œæœ€åå¯åŠ¨åˆ†ææ—¶å°†ä¼šç”¨åˆ°ï¼‰ã€‚226 åˆ° 231 è¡Œå°†æ ¹æ®è°ƒç”¨ catalina.bat æ—¶è®¾ç½®çš„å‚æ•°å†³å®šè·³è½¬ï¼ŒæŒ‰ç…§ä¸Šä¸€æ®µçš„æœ€åçš„è¯´æ˜ï¼Œæ­¤æ—¶å°†ä¼šè·³è½¬åˆ° doStart æ ‡è®°è¡Œã€‚ä» 265 è¡Œå¼€å§‹ï¼Œæ ¡éªŒå’Œè®¾ç½®ä¸€äº›å˜é‡ä¹‹åï¼ˆæ³¨æ„ç¬¬ 269 è¡Œï¼‰å°†ä¼šè·³è½¬åˆ° execCmd æ ‡è®°è¡Œï¼Œå³ 297 è¡Œã€‚297 åˆ° 305 è¡Œä½œç”¨æ˜¯å°†ç³»ç»Ÿå‚æ•°ä¿å­˜åˆ°`CMD_LINE_ARGS`å˜é‡ä¸­ã€‚

æ¥ä¸‹æ¥ä¾¿æ˜¯è°œåº•è§£å¼€çš„æ—¶å€™ï¼Œæ­£å¸¸æƒ…å†µä¸‹å°†ä¼šæ‰§è¡Œåˆ° 310 è¡Œï¼Œè¿™é‡Œå•ç‹¬è´´å‡ºæ¥åˆ†æä¸€ä¸‹ï¼š `%_EXECJAVA% %JAVA_OPTS% %CATALINA_OPTS% %DEBUG_OPTS% -Djava.endorsed.dirs="%JAVA_ENDORSED_DIRS%" -classpath "%CLASSPATH%" -Dcatalina.base="%CATALINA_BASE%" -Dcatalina.home="%CATALINA_HOME%" -Djava.io.tmpdir="%CATALINA_TMPDIR%" %MAINCLASS% %CMD_LINE_ARGS% %ACTION%` æŒ‰ç…§ 202 è¡Œçš„æç¤º`_EXECJAVA`ä¸€å¼€å§‹çš„å€¼ç­‰äº`_RUNJAVA`ï¼Œä½†åœ¨ 269 è¡Œå°†è¯¥å€¼æ”¹æˆäº†`start "%TITLE%" %_RUNJAVA%`ï¼Œè¿™é‡Œçš„ä½œç”¨æ˜¯å¦å¯åŠ¨ä¸€ä¸ªå‘½ä»¤è¡Œçª—å£ï¼Œå¹¶ç»™çª—å£èµ·åï¼Œæœ€ç»ˆè¿˜æ˜¯æ‰§è¡Œ java.exe æ–‡ä»¶ã€‚ä»`%JAVA_OPTS%`å¼€å§‹åˆ°`%MAINCLASS%`ä¹‹å‰éƒ½æ˜¯åœ¨è®¾ç½®è¿è¡Œæ—¶å‚æ•°å’Œç³»ç»Ÿå±æ€§ï¼Œ`%MAINCLASS%`å³ 203 è¡Œçš„ org.apache.catalina.startup.Bootstrapï¼Œåé¢æ˜¯å…¶å®ƒçš„å‘½ä»¤è¡Œå‚æ•°ï¼Œæœ€åä¸€ä¸ªå‚æ•°`%ACTION%`åœ¨ 204 è¡Œæ—¶è¢«è®¾ç½®æˆ startã€‚å½’ç»“èµ·æ¥ä¸€å¥è¯ï¼Œå³æ‰§è¡Œ Bootstrap ç±»çš„ main æ–¹æ³•ã€‚

setclasspath.bat:

```Bash
     1	@echo off
     2	rem Licensed to the Apache Software Foundation (ASF) under one or more
     3	rem contributor license agreements.  See the NOTICE file distributed with
     4	rem this work for additional information regarding copyright ownership.
     5	rem The ASF licenses this file to You under the Apache License, Version 2.0
     6	rem (the "License"); you may not use this file except in compliance with
     7	rem the License.  You may obtain a copy of the License at
     8	rem
     9	rem     http://www.apache.org/licenses/LICENSE-2.0
    10	rem
    11	rem Unless required by applicable law or agreed to in writing, software
    12	rem distributed under the License is distributed on an "AS IS" BASIS,
    13	rem WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    14	rem See the License for the specific language governing permissions and
    15	rem limitations under the License.
    16
    17	rem ---------------------------------------------------------------------------
    18	rem Set JAVA_HOME or JRE_HOME if not already set, ensure any provided settings
    19	rem are valid and consistent with the selected start-up options and set up the
    20	rem endorsed directory.
    21	rem
    22	rem $Id: setclasspath.bat 1202062 2011-11-15 06:50:02Z mturk $
    23	rem ---------------------------------------------------------------------------
    24
    25	rem Make sure prerequisite environment variables are set
    26
    27	rem In debug mode we need a real JDK (JAVA_HOME)
    28	if ""%1"" == ""debug"" goto needJavaHome
    29
    30	rem Otherwise either JRE or JDK are fine
    31	if not "%JRE_HOME%" == "" goto gotJreHome
    32	if not "%JAVA_HOME%" == "" goto gotJavaHome
    33	echo Neither the JAVA_HOME nor the JRE_HOME environment variable is defined
    34	echo At least one of these environment variable is needed to run this program
    35	goto exit
    36
    37	:needJavaHome
    38	rem Check if we have a usable JDK
    39	if "%JAVA_HOME%" == "" goto noJavaHome
    40	if not exist "%JAVA_HOME%\bin\java.exe" goto noJavaHome
    41	if not exist "%JAVA_HOME%\bin\javaw.exe" goto noJavaHome
    42	if not exist "%JAVA_HOME%\bin\jdb.exe" goto noJavaHome
    43	if not exist "%JAVA_HOME%\bin\javac.exe" goto noJavaHome
    44	set "JRE_HOME=%JAVA_HOME%"
    45	goto okJava
    46
    47	:noJavaHome
    48	echo The JAVA_HOME environment variable is not defined correctly.
    49	echo It is needed to run this program in debug mode.
    50	echo NB: JAVA_HOME should point to a JDK not a JRE.
    51	goto exit
    52
    53	:gotJavaHome
    54	rem No JRE given, use JAVA_HOME as JRE_HOME
    55	set "JRE_HOME=%JAVA_HOME%"
    56
    57	:gotJreHome
    58	rem Check if we have a usable JRE
    59	if not exist "%JRE_HOME%\bin\java.exe" goto noJreHome
    60	if not exist "%JRE_HOME%\bin\javaw.exe" goto noJreHome
    61	goto okJava
    62
    63	:noJreHome
    64	rem Needed at least a JRE
    65	echo The JRE_HOME environment variable is not defined correctly
    66	echo This environment variable is needed to run this program
    67	goto exit
    68
    69	:okJava
    70	rem Don't override the endorsed dir if the user has set it previously
    71	if not "%JAVA_ENDORSED_DIRS%" == "" goto gotEndorseddir
    72	rem Set the default -Djava.endorsed.dirs argument
    73	set "JAVA_ENDORSED_DIRS=%CATALINA_HOME%\endorsed"
    74	:gotEndorseddir
    75
    76	rem Set standard command for invoking Java.
    77	rem Note that NT requires a window name argument when using start.
    78	rem Also note the quoting as JAVA_HOME may contain spaces.
    79	set _RUNJAVA="%JRE_HOME%\bin\java"
    80	set _RUNJDB="%JAVA_HOME%\bin\jdb"
    81
    82	goto end
    83
    84	:exit
    85	exit /b 1
    86
    87	:end
    88	exit /b 0

```

# ä¸‰ã€æ€»ç»“

ä»¥ä¸Šå³ tomcat çš„å¯åŠ¨è„šæœ¬ä¸­çš„æ€»ä½“æµç¨‹ï¼Œåªæ˜¯è®²äº†é»˜è®¤çš„è„šæœ¬è¿è¡Œæ–¹å¼ï¼Œä½†åœ¨è„šæœ¬é˜…è¯»ä¸­å¯ä»¥çœ‹åˆ°èƒ½æ ¹æ® catalina.bat é‡Œå¯ä»¥æ ¹æ®ä¸åŒå‚æ•°ä»¥ä¸åŒæ–¹å¼è¿è¡Œ tomcat ï¼š

```Bash
debug             Start Catalina in a debugger
debug -security   Debug Catalina with a security manager
jpda start        Start Catalina under JPDA debugger
run               Start Catalina in the current window
run -security     Start in the current window with security manager
start             Start Catalina in a separate window
start -security   Start in a separate window with security manager
stop              Stop Catalina
configtest        Run a basic syntax check on server.xml

```

åªè¦èƒ½çœ‹æ‡‚åŸºæœ¬ dos å‘½ä»¤ï¼Œè¿™å‡ ä¸ªè„šæœ¬çš„é˜…è¯»å…¶å®å¾ˆç®€å•ï¼Œå…¶ä»– bat æ–‡ä»¶å¯ä»¥å¤§ä½“æµè§ˆä¸€ä¸‹ï¼Œä¼šå‘ç°è½¬äº†åŠå¤©ï¼Œæœ€ç»ˆéƒ½ä¼šæ‰§è¡Œ catalina.bat æ–‡ä»¶ï¼Œè€Œ catalina.bat æ–‡ä»¶é‡Œæœ€ç»ˆä¼šæ‰§è¡Œ Bootstrap æ–‡ä»¶çš„ main æ–¹æ³•ï¼Œä¸åŒçš„æ˜¯è°ƒç”¨ main æ–¹æ³•æ—¶ä¼šå„è‡ªæ·»åŠ ä¸åŒçš„å…¥å‚è€Œå·²ã€‚bat æ–‡ä»¶ç”¨äº windows æ“ä½œç³»ç»Ÿä¸‹å¯åŠ¨ tomcatï¼Œè€Œ sh æ–‡ä»¶åˆ™ç”¨äº unix ç¯å¢ƒä¸‹çš„å¯åŠ¨ï¼ŒåŸç†æ˜¯ä¸€è‡´çš„ã€‚
